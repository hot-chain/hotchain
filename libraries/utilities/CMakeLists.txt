list( APPEND CMAKE_MODULE_PATH "${CMAKE_SOURCE_DIR}/libraries/fc/GitVersionGen" )
include( GetGitRevisionDescription )
get_git_head_revision(GIT_REFSPEC HOTC_GIT_REVISION_SHA)
get_git_unix_timestamp(HOTC_GIT_REVISION_UNIX_TIMESTAMP)
git_describe(HOTC_GIT_REVISION_DESCRIPTION --tags)
if(NOT HOTC_GIT_REVISION_DESCRIPTION)
    set(HOTC_GIT_REVISION_DESCRIPTION "unknown")
endif(NOT HOTC_GIT_REVISION_DESCRIPTION)

file(GLOB HEADERS "include/hotc/utilities/*.hpp")

set(sources
   key_conversion.cpp
   string_escape.cpp
   tempdir.cpp
   words.cpp
   ${HEADERS})

configure_file("${CMAKE_CURRENT_SOURCE_DIR}/git_revision.cpp.in" "${CMAKE_CURRENT_BINARY_DIR}/git_revision.cpp" @ONLY)
list(APPEND sources "${CMAKE_CURRENT_BINARY_DIR}/git_revision.cpp")

add_library( hotc_utilities
             ${sources}
             ${HEADERS} )
target_link_libraries( hotc_utilities fc )
target_include_directories( hotc_utilities
                            PUBLIC "${CMAKE_CURRENT_SOURCE_DIR}/include" )
if (USE_PCH)
  set_target_properties(hotc_utilities PROPERTIES COTIRE_ADD_UNITY_BUILD FALSE)
  cotire(hotc_utilities)
endif(USE_PCH)

install( TARGETS
   hotc_utilities

   RUNTIME DESTINATION bin
   LIBRARY DESTINATION lib
   ARCHIVE DESTINATION lib
)
install( FILES ${HEADERS} DESTINATION "include/hotc/utilities" )
